#!/bin/zsh

autoload -Uz vcs_info

typeset -A prompts
prompts[s]="vcs"
prompts[b]="branch"
prompts[i]="revision"
prompts[c]="staged"
prompts[u]="unstaged"
prompts[R]="dir"
prompts[r]="rep"
prompts[S]="subdir"
prompts[m]="misc"
prompts[a]="action"

typeset -a prompt_opts
for o in ${(k)prompts}; do
    prompt_opts+="-$o"
done
zparseopts -A opts -D -E -K A: p: v ${(k)^prompts}=prompt_opts
if [[ ${1[1]} == "-" ]]; then
    cat 1>&2 << EOF
"Unknown option(s): $*"
Usage: ${0:t} [ -A assoc -p prefix -v -${(k)^prompts} ] [dir]
    -A assoc  put values in associative array'
    -p prefix name variables with prefix
    -v        print out values without names
EOF
    exit 1
fi

p="$opts[-p]"

func addVar() {
    if (( ${+opts[-v]} )) ; then
	echo "%$2"
    else
	if (( ${+opts[-A]} )); then
	    echo -n "$opts[-A][$1]"
	else
	    echo -n "$1"
	fi
	echo "='%$2';"
    fi
}

(( ${+opts[-A]} )) && echo typset -A $opts[-A]\;
for f in "${(@)prompt_opts}"; do
    k=$f[2,2]
    n=$prompts[$k]
    if [[ $f != "-a" ]] then
	fmt="$fmt"$(addVar "$p$n" $k)"\n"
    else
	actfmt="$actfmt"$(addVar "$p$n" $k)"\n"
    fi
done
actfmt+="$fmt"

zstyle ':vcs_info:*' actionformats "$actfmt"
zstyle ':vcs_info:*' formats       "$fmt"

if [[ "$1" != "" ]]; then
    cd $1 || exit 1
fi
vcs_info
echo -n "$vcs_info_msg_0_"
